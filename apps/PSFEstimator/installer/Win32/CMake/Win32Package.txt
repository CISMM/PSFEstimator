# Qt DLLs
SET( QT_DLLS
  "${QT_LIBRARY_DIR}/QtCore4.dll"
  "${QT_LIBRARY_DIR}/QtGui4.dll"
  "${QT_LIBRARY_DIR}/QtNetwork4.dll"
  "${QT_LIBRARY_DIR}/QtSql4.dll"
  "${QT_LIBRARY_DIR}/QtWebKit4.dll"
  "${QT_LIBRARY_DIR}/QtXmlPatterns4.dll"
  "${QT_LIBRARY_DIR}/phonon4.dll"
  "${VTK_LIBRARY_DIRS}/release/QVTK.dll"
  )
FOREACH ( QT_LIB ${QT_DLLS} )
  IF ( EXISTS ${QT_LIB} )
    INSTALL( FILES ${QT_LIB} DESTINATION bin )
  ENDIF ( EXISTS ${QT_LIB} )
ENDFOREACH ( QT_LIB )

# This should work, but seems to prevent other files from being written 
# into the bin directory in the installation tree.
#SET(CPACK_INSTALL_CMAKE_PROJECTS "${CPACK_INSTALL_CMAKE_PROJECTS};${VTK_BINARY_DIR};VTK;ALL;/")

# VTK DLLs
FILE( GLOB VTK_DLLS "${VTK_LIBRARY_DIRS}/release/vtk*.dll" )
INSTALL( FILES ${VTK_DLLS} DESTINATION bin )

# libxml2 DLL
# First, find which one VTK was built with
FILE( READ "${VTK_LIBRARY_DIRS}/../CMakeCache.txt" VTK_CMAKE_CACHE )
STRING( REGEX REPLACE ".*LIBXML2_LIBRARIES:FILEPATH=([^\n]+).*" "\\1" LIBXML2_DLL ${VTK_CMAKE_CACHE} )
STRING( REPLACE ".lib" ".dll" LIBXML2_DLL ${LIBXML2_DLL} )
INSTALL( FILES "${LIBXML2_DLL}" DESTINATION bin )

# ITK DLLs
FILE( GLOB ITK_DLLS "${ITK_LIBRARY_DIRS}/release/ITKCommon.dll" )
INSTALL( FILES ${ITK_DLLS} DESTINATION bin )

SET( USER_GUIDE_NAME "PSFEstimator-${PSFEstimator_VERSION_STRING}-UserGuide.pdf" )
SET( USER_GUIDE_FILE   "${PSFEstimator_BINARY_DIR}/docs/${USER_GUIDE_NAME}" )
IF ( EXISTS "${USER_GUIDE_FILE}" )

  INSTALL( FILES
  "${PSFEstimator_BINARY_DIR}/docs/${USER_GUIDE_NAME}"
    DESTINATION . )
  SET( CPACK_NSIS_MENU_LINKS 
    "${USER_GUIDE_NAME}" "PSF Estimator ${PSFEstimator_VERSION_STRING} User Guide" 
    "http://www.cismm.org/downloads/?dl_cat=5" "PSF Estimator Download Site"
    "http://www.cismm.org/" "CISMM Web Site"
  )

ELSE ( EXISTS "${USER_GUIDE_FILE}" )

  MESSAGE( WARNING "Could not find the user guide PDF file. Please generate it by running LaTeX on ${PSFEstimator_BINARY_DIR}/docs/PSFEstimator-${PSFEstimator_VERSION_STRING}-UserGuide.tex" )

ENDIF ( EXISTS "${USER_GUIDE_FILE}" )

SET( CPACK_INSTALL_DIRECTORY "$PROGRAMFILES\\\\CISMM" )

# Save time by just using the NSIS generator
SET( CPACK_GENERATOR "NSIS" )

# Installer-specific things
SET( CPACK_NSIS_URL_INFO_ABOUT "http://www.cismm.org" )
SET( CPACK_NSIS_CONTACT "cquammen@cs.unc.edu" )

# Both of these need to be set or neither of them is set.
SET( CPACK_NSIS_MUI_ICON "${PSFEstimator_SOURCE_DIR}/apps/PSFEstimator/installer/Win32/Win32Icon.ico" )
SET( CPACK_NSIS_MUI_UNIICON "${PSFEstimator_SOURCE_DIR}/apps/PSFEstimator/installer/Win32/ModernUninstallFull.ico" )
SET( CPACK_NSIS_INSTALLED_ICON_NAME "BIN\\\\PSFEstimator.exe" )
